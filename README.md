# ミドリ (Midori)

A statically-typed functional programming language featuring algebraic data types, pattern matching, typeclasses, and a module system. Compiles to bytecode for the Midori Virtual Machine with garbage-collected memory management.

## Key Features

- **Static Type System** - Strong static typing with bidirectional inference, generics, and constraints
- **Pattern Matching** - Exhaustive pattern matching on unions and `Bool`
- **Algebraic Data Types** - Structs (product types) and unions (sum types)
- **Type Aliases** - Create readable names for complex types
- **Typeclasses** - Constrained generics with associated types and instance dispatch
- **Deriving** - Generate structural and container helpers from type declarations
- **Module System** - Explicit imports/exports with privacy enforcement
- **Package System** - Early manifest-based package loading with native FFI bindings
- **Pipe Operator** - Functional composition with `|>`, inferred lambdas, and `|> match with`
- **Ranges** - Elegant `start..step..end` syntax for loops
- **Closures** - First-class functions with lexical scoping
- **Expression-Oriented** - Everything is an expression with a value

Current scope note: there is no `async` / `await` surface in the current language.

## Quick Start

## Installation (Windows)
```powershell
# From the repo root (after building Midori.exe):
python .\scripts\install.py --copy-binaries

# This prefers a Release preset build if present,
# otherwise falls back to Development/Debug. Use --preset to force:
# python .\scripts\install.py --copy-binaries --preset x64-release

# Uninstall:
# python .\scripts\uninstall.py
```

## Getting Started

After `Midori.exe` is on your `PATH`, a basic workflow uses only the CLI:

```powershell
midori init hello-world
cd hello-world

midori check src/Main.mdr
midori fmt src -w
midori run src/Main.mdr
midori test
```

Every `.mdr` source file must begin with an explicit `module` declaration. Short snippets below may omit it for brevity, but complete file examples include it.

### Hello World
```midori-test name=readme/hello_world path=.doc_examples/readme_hello_world.mdr
module Main

// Path import (relative or absolute)
import { "../MidoriPrelude/IO.mdr" }

IO::PrintLine("Hello, Midori!");
```

### Basic Types & Variables
```midori
def number : Int = 42;
def pi : Float = 3.14159;
def message : Text = "Hello";
def flag : Bool = true;
def items : Array<Int> = [1, 2, 3, 4, 5];
```

### Tuples and Destructuring
```midori-test name=readme/tuples_destructuring path=.doc_examples/readme/tuples_destructuring.mdr module=ReadmeTuplesDestructuring
def pair = (42, "answer");
def (count, label) = pair;
```

### Functions
```midori
// Simple function
def square = fn(x: Int) : Int => {
    return x * x;
};

// Function with type inference
def add = fn(a: Int, b: Int) : Int => a + b;

// Generic function
def identity = fn<T>(value: T) : T => value;

// Higher-order function
def apply = fn<T, R>(fn: fn(T) -> R, value: T) : R => {
    return fn(value);
};
```

### Control Flow
```midori
// If-else expression
def result = if x > 0 then "positive" else "non-positive";

// For loop with ranges
for i in 0..1..10 {
    IO::PrintLine(i as Text);
};

// For loop with arrays
def names = ["Alice", "Bob", "Charlie"];
for name in names {
    IO::PrintLine(name);
};

// Array length operator
def arr = [1, 2, 3, 4, 5];
def len = #arr;  // 5

// Loop with break
def sum = loop {
    if count >= 10 then break total else ();
    total = total + count;
    count = count + 1;
};
```

### Records (Product Types)
```midori
type Point = {
    x: Float,
    y: Float,
};

type Box<T> = {
    value: T,
};

def origin = Point(0.0, 0.0);
def boxed = Box(42);
def x_coord = origin.x;
```

### Sums (Union Types)
```midori
type Option<T> = None | Some(T);

type List<T> = Cons(T, List<T>) | Nil;

def maybe_value = Option::Some(42);
def empty_value : Option<Int> = Option::None();
def empty_list : List<Int> = List::Nil();
```

### Type Aliases
```midori
// Basic type aliases
type UserId = Int;
type Name = Text;

def user_id: UserId = 42;
def user_name: Name = "Alice";

// Type alias for a record
type Point = { x: Float, y: Float };
type Position = Point;

def pos: Position = Point(10.0, 20.0);

// Generic type alias
type Pair<A, B> = { first: A, second: B };
type IntPair = Pair<Int, Int>;

def coords: IntPair = Pair(1, 2);
```

### Pattern Matching
```midori
type Result<T, E> = Ok(T) | Err(E);

def handle_result = fn<T>(result: Result<T, Text>) : Text => {
    return match result with
        case Result::Ok(value) => "Success: " ++ (value as Text)
        case Result::Err(msg) => "Error: " ++ msg
    ;
};
```

### Typeclasses
```midori
// Define a typeclass
class Show<T> {
    show: fn(value: T) -> Text;
};

// Implement for Int
instance Show<Int> {
    def show = fn(value: Int) : Text => {
        return value as Text;
    };
};

// Use with constraints
def display = fn<T>(value: T) : Text where Show<T> => {
    return Show::show(value);
};

def message = display(42);  // "42"
```

### Associated Types
```midori
type Option<T> = None | Some(T);

class Iterable<Iter> {
    type Item;
    Next: fn(iter: Iter) -> Option<Item>;
};

def NextValue = fn<Iter>(iter: Iter) : Option<Iterable::Item<Iter>>
    where Iterable<Iter> => {
    return Iterable::Next(iter);
};
```

### Deriving
```midori
type Point = {
    x: Int,
    y: Int
} deriving (Equatable, Hashable);

type OptionBox<T> = Empty | Full(T) deriving (Map, Bind, Unwrap);
```

### Module System
```midori
// Define a module (MyModule.mdr)
module MyModule
public export { add, multiply }

def add = fn(a: Int, b: Int) : Int => a + b;
def multiply = fn(a: Int, b: Int) : Int => a * b;
def internal = fn() : Int => 100;  // Not exported

// Use in another file
// Path import (relative or absolute)
import { "./MyModule.mdr" }

// Or search in MIDORI_PATH (semicolon-separated on Windows, colon-separated on Unix)
// import { <MyModule> }

def result = MyModule::add(5, 3);
```

### Package System

Midori has early package support for manifest-discovered modules and optional native FFI libraries loaded at import time. This is not yet a full package manager.

**Package structure:**
```text
PackageName/
  package.midori
  PackageName.mdr
  lib/
    windows/x64/packagename.dll
```

`ModuleManager` discovers `package.midori` next to the imported module file. Actual exported symbols still come from the module's `public export` / `private export` blocks.

**Using a package:**
```bash
# Set MIDORI_PATH to include package roots.
# Use ';' on Windows and ':' on Unix-like systems.
export MIDORI_PATH="/path/to/packages/PackageName:/path/to/MidoriPrelude"
```

```midori
import { <PackageName> }

def result = PackageName::NativeFunction(arg1, arg2);
```

See [Package System](docs/package-system.md) for the current manifest fields, dynamic-loading behavior, and FFI ABI limits.

### Pipe Operator
```midori
def double = fn(x: Int) : Int => x * 2;
type Result<T, E> = Ok(T) | Err(E);

def transform = fn(value: Int) : Result<Int, Text> => {
    if value > 10
    then Result::Ok(value + 1)
    else Result::Err("too small")
};

def result =
    5
    |> double
    |> fn(x) => { x + 1 }
    |> transform
    |> match with
        case Result::Ok(value) => value
        case Result::Err(_) => 0;
```

In pattern position, `_` is a wildcard that ignores the matched value and does not bind a local. Outside patterns, `_` remains a normal identifier.

### Closures
```midori
def make_counter = fn() : fn() -> Int => {
    def count = 0;
    return fn() : Int => {
        count = count + 1;
        return count;
    };
};

def counter = make_counter();
def first = counter();   // 1
def second = counter();  // 2
```

## Language Features

### Type System
- **Primitive Types**: `Int`, `Float`, `Byte`, `Word`, `Bool`, `Text`, `Unit`
- **Composite Types**: `Array<T>`, tuples, structs, unions
- **Function Types**: `fn(T1, T2) -> R`
- **Type Aliases**: `type UserId = Int;` for readable type names
- **Generic Parameters**: Single and multiple type parameters
- **Type Constraints**: `where` constraints on functions, structs, and unions
- **Associated Types**: Projections such as `Iterable::Item<Iter>`
- **Deriving**: `Equatable`, `Hashable`, `Map`, `Bind`, and `Unwrap`
- **Type Inference**: Automatic type deduction for instantiation, constructors, and context-aware lambdas
- **Tuple Destructuring**: `def (x, y) = pair;`

#### Numeric Limits

**Integer (Int)** - 64-bit signed integer:
- Maximum value: `9223372036854775807` (2^63 - 1)
- Minimum value: `-9223372036854775807` (practical limit)
- **Note**: The literal `-9223372036854775808` (INT64_MIN) causes a parser overflow and should be avoided

**Float** - Double-precision floating point:
- Range: Approximately ±1.7E+308
- Precision: ~15-17 decimal digits

**Unicode Text Support**:
- Full UTF-8 encoding support
- String length returns code point count (not byte count)
- Supports multi-byte characters from all Unicode planes

### Operators
- **Arithmetic**: `+`, `-`, `*`, `/`, `%`
- **Comparison**: `==`, `!=`, `<`, `>`, `<=`, `>=`, with typeclass dispatch for user-defined types through `Equatable<T>` and `Orderable<T>`
- **Logical**: `&&`, `||`, `!`
- **Bitwise**: `&`, `|`, `^`, `<<`, `>>`
- **Casts**: `as`, with builtin primitive conversions and constrained dispatch through `Convertable<From, To>`
- **Concatenation**: `++` for `Text` and `Array<T>`, with constrained dispatch through `Concatenable<T>`
- **Pipe**: `|>` (function composition)
- **Length**: `#` for arrays and other countable shapes, with constrained dispatch through `Countable<T>`
- **Compound Assignment**: `+=`, `-=`, `*=`, `/=`, `%=`, `&=`, `|=`, `^=`, `<<=`, `>>=`

Concatenation assignment is explicit rather than a dedicated operator:

```midori
items = items ++ [value];
text = text ++ suffix;
Prependable::Prepend(items, value);
Appendable::Append(items, value);
Extendable::Extend(items, other_items);
```

### Advanced Features
- **Recursive Data Types**: Self-referential unions for lists, trees
- **Exhaustive Matching**: Compiler-enforced pattern coverage
- **Constructor Inference**: Generic constructor arguments can be inferred from arguments and context
- **Pipe Into Match**: Pipelines can flow directly into `match with`
- **Range Expressions**: `start..step..end` with positive/negative steps
- **Array Iteration**: `for x in array` iterates over elements
- **Float Ranges**: Support for decimal step values
- **Nested Generics**: Complex generic type compositions
- **Cross-Module Typeclasses**: Import and use classes, instances, and associated types across modules

## Standard Library

`MidoriPrelude` is the standard-library layer that ships with the repo. The main module groups are:

- **Core ADTs** - `Prelude/Option.mdr`, `Prelude/Result.mdr`, `Prelude/List.mdr`
- **Collections** - `Collections/Map.mdr`, `Collections/Set.mdr`
- **Effects** - `IO.mdr`, `System.mdr`, `DateTime.mdr`
- **Built-in helpers** - `TextUtil.mdr`, `ArrayUtil.mdr`, `Math.mdr`
- **Helper / typeclass modules** - `Appendable`, `Prependable`, `Extendable`, `Concatenable`, `Convertable`, `Countable`, `Equatable`, `Hashable`, `Iterable`, `Orderable`, `Prelude/Panic`

The public IO and system surface now prefers typed wrappers over sentinel values. Common entry points include:

- `IO::TryReadFile`, `IO::TryWriteFile`, `IO::TryAppendToFile`, `IO::TryDeleteFile`, `IO::TryRenameFile`, and `IO::TryGetFileSize`
- `System::TryGetEnv`, `System::CurrentDirectory`, `System::TrySetCurrentDirectory`, `System::Run`, `System::CurrentPlatform`, and `System::CurrentProcessId`
- `DateTime::LocalNow`, `DateTime::UtcNow`, `DateTime::NowUnixMillis`, and `DateTime::FormatLocal`
- `TextUtil::Length`, `TextUtil::Split`, `TextUtil::Replace`, `TextUtil::Trim`, and `TextUtil::Reverse`
- `ArrayUtil::Append`, `ArrayUtil::Prepend`, `ArrayUtil::Extend`, `ArrayUtil::Slice`, and `ArrayUtil::Reverse`

`Prelude/Result.mdr` uses `Result::Ok` and `Result::Err`. The older `Result::OK` and `Result::Error` spellings are removed from the public prelude API.

See [Prelude](docs/prelude.md) for module-by-module notes and examples covering the typed `IO`, `System`, `DateTime`, `TextUtil`, `ArrayUtil`, and helper/typeclass modules.

## Foreign Function Interface (FFI)

Midori has two FFI call paths:

- `CALL_FOREIGN_INDEXED` for built-in runtime functions registered in `MidoriFFIRegistry`
- `CALL_FOREIGN` for generic external functions, including package-provided dynamic libraries

The examples below describe the generic `CALL_FOREIGN` ABI used by ordinary external and package functions.

### Declaring Foreign Functions

Use the `foreign` keyword to declare external functions:

```midori
// Declare a foreign function
foreign "MIDORI_FFI_Print" Print : fn(Text) -> Unit;

// With multiple parameters
foreign "MIDORI_FFI_WriteFile" WriteFile : fn(Text, Text) -> Bool;

// Returning complex types
foreign "MIDORI_FFI_ReadBinaryFile" ReadBinaryFile : fn(Text) -> Array<Byte>;
```

### Supported Surface

- Raw scalars: `Int`, `Float`, `Bool`, `Byte`, `Word`, `Unit`
- `Text`
- `Array<T>`

### FFI Function Signature

All FFI functions must follow this signature:

```cpp
extern "C" {
    MIDORI_STDLIB_API void MIDORI_FFI_FunctionName(void** args, void* ret) noexcept;
}
```

`ret` always points at an 8-byte return slot. `args` is a `void**`, but generic dynamic calls do not carry the richer builtin metadata from `CALL_FOREIGN_INDEXED`.

### Type Marshalling

#### Raw Scalar Types

For generic dynamic FFI, raw scalar bits are stored in the pointer-sized `args[i]` slot itself. Read them by copying from `&args[i]`, not by treating `args[i]` as a pointer to the scalar value.

```cpp
int64_t value;
std::memcpy(&value, &args[0], sizeof(value));

bool flag;
std::memcpy(&flag, &args[1], sizeof(flag));
```

Return raw scalars by copying the value bytes into `ret`:

```cpp
int64_t result = 42;
std::memcpy(ret, &result, sizeof(result));

bool success = true;
std::memcpy(ret, &success, sizeof(success));

// Unit
std::memset(ret, 0, sizeof(double));
```

#### Text Type

Receive text arguments as a C string pointer:

```cpp
const char* text = static_cast<const char*>(args[0]);
```

Return text as a `malloc`-allocated `char*` written into the 8-byte return slot:

```cpp
const size_t size = std::strlen(data) + 1;
char* result = static_cast<char*>(std::malloc(size));
std::memcpy(result, data, size);

const int64_t ptr = reinterpret_cast<int64_t>(result);
std::memcpy(ret, &ptr, sizeof(int64_t));
```

The VM copies returned text into Midori-managed storage and then frees the original string.

#### Array Type

Receive arrays through an array-view struct:

```cpp
struct ArrayArgument {
    void* data;
    int length;
};

const ArrayArgument* array = static_cast<const ArrayArgument*>(args[0]);
```

Return arrays as a heap-allocated wrapper pointing at a heap-allocated element buffer:

```cpp
struct FFIArray {
    void* data;
    int length;
};

std::uint64_t* array_data =
    static_cast<std::uint64_t*>(std::malloc(length * sizeof(std::uint64_t)));

for (int i = 0; i < length; i++) {
    int64_t value = i * 10;
    std::memcpy(&array_data[i], &value, sizeof(value));
}

FFIArray* result = static_cast<FFIArray*>(std::malloc(sizeof(FFIArray)));
result->data = array_data;
result->length = length;

const int64_t ptr = reinterpret_cast<int64_t>(result);
std::memcpy(ret, &ptr, sizeof(int64_t));
```

For flat scalar arrays, each element slot should use Midori's 8-byte runtime value layout.

### Memory Management Rules

- Use `malloc` / `free` compatible allocation for returned text and arrays.
- Do not free returned buffers after writing their pointer into `ret`; the VM takes ownership.
- Returned arrays are wrapped through `MidoriArray::FromFFI`.
- Short returned arrays are copied into Midori small-object storage and their original FFI buffer is freed.
- Longer returned arrays are adopted directly without an element copy.
- The outer `FFIArray` wrapper itself is always freed by the VM.
- FFI code should not access the VM's garbage collector or internal runtime objects directly.
- Return values still have to fit in the 8-byte `ret` slot.

See [Package System](docs/package-system.md) for the current manifest-driven loading flow, runtime lookup order, and dynamic ABI limits.

## Development

### Building Midori

Midori uses CMake presets for native builds.

Configure and build a Development binary:
```bash
cmake --preset x64-development
cmake --build --preset x64-development --target Midori
```

Other common presets:
```bash
cmake --preset x64-debug
cmake --build --preset x64-debug --target Midori

cmake --preset x64-release
cmake --build --preset x64-release --target Midori
```

Native preset builds write the executable to `out/build/ninja/<preset>/out/Midori.exe`.

### Running Programs

```bash
# Run a Midori program built with the Development preset
.\out\build\ninja\x64-development\out\Midori.exe run path\to\program.mdr

# Shorthand form
.\out\build\ninja\x64-development\out\Midori.exe path\to\program.mdr

# Type-check only
.\out\build\ninja\x64-development\out\Midori.exe check path\to\program.mdr

# Compile without executing
.\out\build\ninja\x64-development\out\Midori.exe build path\to\program.mdr

# This emits path\to\program.mbc.json next to the source file
```

### Running Unit Tests

Debug and Development preset builds enable `MIDORI_BUILD_TESTS` by default. Release preset builds leave unit tests off unless you opt in with `-DMIDORI_BUILD_TESTS=ON`.

See [Testing Guide](docs/testing.md) for the `tests/` vs `test/` split, helper usage, and the full command matrix.

For a single entry point that configures, builds, and runs tests, use:
```bash
python scripts/test_project.py
```

Configure and build the unit test target:
```bash
cmake --preset x64-debug
cmake --build --preset x64-debug --target MidoriUnitTests
```

Run the registered Catch2 suites through CTest:
```bash
ctest --test-dir out/build/ninja/x64-debug --output-on-failure
```

You can also build and run the Development preset:
```bash
cmake --preset x64-development
cmake --build --preset x64-development --target MidoriUnitTests
ctest --test-dir out/build/ninja/x64-development --output-on-failure
```

### Running Regression Tests

See [Testing Guide](docs/testing.md) for when a new test should go in `tests/` instead of `test/`, plus filtering examples for both harnesses.

Run all file-based language regression tests:
```bash
.\out\build\ninja\x64-development\out\Midori.exe test
```

Run specific regression tests:
```bash
.\out\build\ninja\x64-development\out\Midori.exe test --test closure/simple.mdr
.\out\build\ninja\x64-development\out\Midori.exe test typeclass
.\out\build\ninja\x64-development\out\Midori.exe test static_analyzer
.\out\build\ninja\x64-development\out\Midori.exe test --pattern recursive
```

Legacy Python runners are still available:

```bash
python scripts/run_tests.py --build Development
python scripts/test_project.py
```

Test fixtures are file-based:
```bash
# Add a new program test under test/<category>/<name>.mdr
# Put tests under a failure/ directory when they should fail compilation
# Add <name>.expected to assert stdout/stderr or compile-fail diagnostic snapshots
# Snapshot comparison strips ANSI codes and repo-root path prefixes first
# Add <name>.warnings.json to assert warning code, line, and message fragments
```

## Example Programs

### Recursive Fibonacci
```midori-test name=readme/recursive_fibonacci path=.doc_examples/readme/recursive_fibonacci.mdr module=ReadmeRecursiveFibonacci
def fib = fn(n: Int) -> Int => {
    return if n <= 1 then n else fib(n - 1) + fib(n - 2);
};
```

### Binary Tree
```midori
type Tree<T> = Leaf(T) | Node(Tree<T>, Tree<T>);

def height = fn<T>(tree: Tree<T>) : Int => {
    return match tree with
        case Tree::Leaf(_) => 1
        case Tree::Node(left, right) => {
            def left_height = height(left);
            def right_height = height(right);
            return 1 + (if left_height > right_height
                        then left_height
                        else right_height);
        }
    ;
};
```

### Generic Linked List
```midori
type List<T> = Cons(T, List<T>) | Nil;

def length = fn<T>(list: List<T>) : Int => {
    return match list with
        case List::Cons(head, tail) => 1 + length(tail)
        case List::Nil => 0
    ;
};

def map = fn<A, B>(list: List<A>, f: fn(A) -> B) : List<B> => {
    return match list with
        case List::Cons(head, tail) =>
            List::Cons(f(head), map(tail, f))
        case List::Nil =>
            List::Nil()
    ;
};
```

## Architecture

- **Frontend**: Lexer → Module Manager → Parser → Type Checker → Static Analyzer
- **Optimizer**: Constant folding, strength reduction, constant branch elimination, local constant propagation, dead code elimination, canonicalization cleanup, closure lifting, and tail call optimization; rerun until a fixpoint or the 8-iteration cap is reached
- **Backend**: Bytecode generator → Linker
- **Runtime**: Single `VirtualMachine` execution path with a non-moving, generational (bitmap mark-sweep) garbage collector

See [Runtime Architecture](docs/runtime-architecture.md) for details on VM execution, closure capture, and memory management.

## Documentation

See the [docs](docs/) folder for detailed technical documentation:

- [Type System](docs/type-system.md) - Type inference, type classes, and algebraic data types
- [Prelude](docs/prelude.md) - Standard-library module map and typed wrapper examples
- [Compilation Workflow](docs/compilation-workflow.md) - Complete pipeline from lexing to linking
- [Feature Matrix](docs/feature-matrix.md) - Current feature status, stability levels, and primary automated coverage
- [Versioning Policy](docs/versioning-policy.md) - Compatibility rules for releases, deprecation, and breaking changes
- [Error Reporting](docs/error-reporting.md) - Structured diagnostics, warning/error codes, and machine-readable output
- [Diagnostic Format](docs/diagnostic-format.md) - Stable JSON envelope and diagnostic object schema for tooling
- [Formatting](docs/formatting.md) - Canonical formatter usage and current scope
- [Package System](docs/package-system.md) - Creating and using packages with native FFI bindings
- [Project Standard](docs/project-standard.md) - Standard project layout and manifest
- [Runtime Architecture](docs/runtime-architecture.md) - VM execution, closure capture, and garbage collection
- [Testing Guide](docs/testing.md) - Choosing between implementation tests and regression tests, with helper and command references

Editor assets:

- `tools/vscode/midori-lang/` - sample VSCode extension with syntax highlighting and on-save diagnostics
